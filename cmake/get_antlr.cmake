# CMake module that downloads the antlr source and builds the C++ runtime required when linking
# the generated lexer/parser. Creates the variable ANTLR_INCLUDE_DIRS to add to your target's
# include directories and allows your build to link against antlr4-runtime.

# Set the directory for tools
file(TO_CMAKE_PATH "${CMAKE_BINARY_DIR}/tools" TOOL_DIR) # Join dir.
set(TOOL_DIR ${TOOL_DIR} CACHE PATH "Tool download directory (ANTLR).") # Set for internal use.
file(TO_NATIVE_PATH "${TOOL_DIR}" TOOL_DIR_NATIVE) # Transform for display.
message(STATUS "Downloaded tools destination: ${TOOL_DIR_NATIVE}")

# Download ANTLR executable, saves us from ensuring people have java build tools (e.g. Maven)...
file(TO_CMAKE_PATH "${TOOL_DIR}/antlr.jar" ANTLR_JAR)
if (NOT EXISTS "${ANTLR_JAR}")
  file(
    DOWNLOAD
    http://www.antlr.org/download/antlr-4.7.1-complete.jar
    "${ANTLR_JAR}"
    SHOW_PROGRESS
  )
endif()

# Download ANTLR source to get the runtime...
# Turn on logging as necessary.
ExternalProject_Add(
  antlr

  # Set up custom paths
  PREFIX "${CMAKE_BINARY_DIR}"
  TMP_DIR "${CMAKE_BINARY_DIR}/antlr-tmp"
  STAMP_DIR "${CMAKE_BINARY_DIR}/antlr-stamp"
  DOWNLOAD_DIR "${CMAKE_BINARY_DIR}"
  SOURCE_DIR "${CMAKE_BINARY_DIR}/antlr"
  BINARY_DIR "${CMAKE_BINARY_DIR}/antlr-build"
  INSTALL_DIR "${CMAKE_BINARY_DIR}/antlr-install"

  # ANTLR repository and tag
  GIT_REPOSITORY https://github.com/antlr/antlr4.git
  GIT_TAG "4.7.1"
  UPDATE_COMMAND ""
  PATCH_COMMAND ""
  TIMEOUT 10
  #LOG_DOWNLOAD ON

  # Modify the configure command to always build release mode, both shared and static libs, notify
  # of jar location, and then set custom paths for the build and install
  CONFIGURE_COMMAND
  ${CMAKE_COMMAND}
  -DCMAKE_BUILD_TYPE=Release
  -DANTLR4CPP_JAR_LOCATION=${ANTLR_JAR}
  -DBUILD_SHARED_LIBS=ON -BUILD_TESTS=OFF
  -DCMAKE_SOURCE_DIR:PATH=<SOURCE_DIR>/runtime/Cpp
  -DCMAKE_INSTALL_PREFIX:PATH=<INSTALL_DIR>
  <SOURCE_DIR>/runtime/Cpp
  #LOG_CONFIGURE ON

  # No need to edit the build command, the autogenerated one works perfectly.
  #LOG_BUILD ON

  # No need to edit the install command, the autogenerated one works perfectly.
  #LOG_INSTALL ON
)

# Grab the install directory so we can get the include directories and built libs.
ExternalProject_Get_Property(antlr INSTALL_DIR)

# Create includes paths
list(APPEND ANTLR_INCLUDE_DIRS ${INSTALL_DIR}/include/antlr4-runtime)
foreach(src_path misc atn dfa tree support)
  list(APPEND ANTLR_INCLUDE_DIRS ${INSTALL_DIR}/include/antlr4-runtime/${src_path})
endforeach(src_path)

# Create libs path and then add it to the linker paths
set(ANTLR_LIBS "${INSTALL_DIR}/lib")
link_directories("${ANTLR_LIBS}")
